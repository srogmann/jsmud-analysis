/*
 * build.gradle of jsmud-analysis.
 *
 * -PapplyJsmudShadow=true: Build with asm included (.class and source!) as shadow-dependency.
 */

buildscript {
    repositories {
        mavenLocal()
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

def isApplyJsmudShadow = (hasProperty('applyJsmudShadow') && applyJsmudShadow == 'true');
if (isApplyJsmudShadow) {
	apply plugin: 'JsmudShadowPlugin'
	JsmudShadowPlugin {
	    depPackage = 'org.objectweb.asm'
	    depPackageShadow = 'org.rogmann.jsmud.shadow.asm'
	}
}
else {
	println "JsmudShadowPlugin is not applied."
}

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
	if (isApplyJsmudShadow) {
	    compileOnly 'org.ow2.asm:asm:9.2'
	    compileOnly 'org.ow2.asm:asm-tree:9.2'
	    compileOnly 'org.ow2.asm:asm-analysis:9.2'
	} else {    
	    implementation 'org.ow2.asm:asm:9.2'
	    implementation 'org.ow2.asm:asm-tree:9.2'
	    implementation 'org.ow2.asm:asm-analysis:9.2'
    }

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
}

group = 'org.rogmann.jsmud'
def snapshotSuffix = rootProject.hasProperty('release') ? '' : '-SNAPSHOT'
version = '0.6.0' + snapshotSuffix
def descriptionSuffix = isApplyJsmudShadow ? ' (asm included)' : ''
description = "Java simulator and multi-user debugger in order to analyze execution of bytecode${descriptionSuffix}"
java.sourceCompatibility = JavaVersion.VERSION_1_8
java.targetCompatibility = JavaVersion.VERSION_1_8

def resLicense = isApplyJsmudShadow ? "res_license_all" : "res_license"
sourceSets {
    main {
        resources {
            srcDirs = ["src/main/resources", "src/main/${resLicense}"]
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

apply plugin: 'maven-publish'
apply plugin: 'signing'
publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
              name = jar.archiveBaseName
              description = project.description
              inceptionYear = '2021'
              licenses {
                license {
                  name = 'Apache License, Version 2.0'
                  url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                }
              }
              url = 'https://github.com/srogmann/jsmud-analysis/'
              issueManagement {
                url = 'https://github.com/srogmann/jsmud-analysis/issues'
              }
              scm {
                connection = 'scm:git:git://github.com/srogmann/jsmud-analysis.git'
                developerConnection = 'scm:git:ssh://github.com:srogmann/jsmud-analysis.git'
                url = 'https://github.com/srogmann/jsmud-analysis/tree/master'
              }
              developers {
                developer {
                  name = 'Sascha Rogmann'
                  id = 'srogmann'
                  roles = ['Creator', 'Java Developer']
                }
              }
            }
        }
    }
}

signing {
    required rootProject.hasProperty('release')
    sign publishing.publications.mavenJava
}


